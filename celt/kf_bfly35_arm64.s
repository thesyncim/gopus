//go:build arm64 && !purego

#include "textflag.h"

// func kfBfly3M1(fout []kissCpx, tw []kissCpx, fstride, n, mm int)
TEXT ·kfBfly3M1(SB), NOSPLIT, $0-72
	MOVD	fout_base+0(FP), R0
	MOVD	tw_base+24(FP), R1
	MOVD	fstride+48(FP), R2
	MOVD	n+56(FP), R3
	MOVD	mm+64(FP), R4
	CMP	$0, R3
	BLE	kf_bfly3_done

	MOVD	R2, R5
	LSL	$3, R5
	ADD	R1, R5, R5
	FMOVS	4(R5), F8
	FMOVS	$0.5, F9

	MOVD	$0, R5

kf_bfly3_loop:
	MOVD	R5, R6
	MUL	R4, R6, R6
	LSL	$3, R6
	ADD	R0, R6, R6

	FMOVS	(R6), F0
	FMOVS	4(R6), F1
	FMOVS	8(R6), F2
	FMOVS	12(R6), F3
	FMOVS	16(R6), F4
	FMOVS	20(R6), F5

	FADDS	F4, F2, F6
	FADDS	F5, F3, F7
	FSUBS	F4, F2, F10
	FSUBS	F5, F3, F11

	FMULS	F6, F9, F14
	FMULS	F7, F9, F15
	FSUBS	F14, F0, F12
	FSUBS	F15, F1, F13

	FMULS	F10, F8, F10
	FMULS	F11, F8, F11

	FADDS	F6, F0, F0
	FADDS	F7, F1, F1

	FADDS	F11, F12, F4
	FSUBS	F10, F13, F5
	FSUBS	F11, F12, F2
	FADDS	F10, F13, F3

	FMOVS	F0, (R6)
	FMOVS	F1, 4(R6)
	FMOVS	F2, 8(R6)
	FMOVS	F3, 12(R6)
	FMOVS	F4, 16(R6)
	FMOVS	F5, 20(R6)

	ADD	$1, R5, R5
	CMP	R5, R3
	BLT	kf_bfly3_loop

kf_bfly3_done:
	RET

// func kfBfly5M1(fout []kissCpx, tw []kissCpx, fstride, n, mm int)
TEXT ·kfBfly5M1(SB), NOSPLIT, $0-72
	MOVD	fout_base+0(FP), R0
	MOVD	tw_base+24(FP), R1
	MOVD	fstride+48(FP), R2
	MOVD	n+56(FP), R3
	MOVD	mm+64(FP), R4
	CMP	$0, R3
	BLE	kf_bfly5_done

	MOVD	R2, R5
	LSL	$3, R5
	ADD	R1, R5, R5
	FMOVS	(R5), F10
	FMOVS	4(R5), F11

	MOVD	R2, R6
	LSL	$1, R6
	LSL	$3, R6
	ADD	R1, R6, R6
	FMOVS	(R6), F12
	FMOVS	4(R6), F13

	MOVD	$0, R5

kf_bfly5_loop:
	MOVD	R5, R6
	MUL	R4, R6, R6
	LSL	$3, R6
	ADD	R0, R6, R6

	FMOVS	(R6), F0
	FMOVS	4(R6), F1
	FMOVS	8(R6), F2
	FMOVS	12(R6), F3
	FMOVS	16(R6), F4
	FMOVS	20(R6), F5
	FMOVS	24(R6), F6
	FMOVS	28(R6), F7
	FMOVS	32(R6), F8
	FMOVS	36(R6), F9

	FMOVS	F0, F22
	FMOVS	F1, F23

	FADDS	F8, F2, F14
	FADDS	F9, F3, F15
	FSUBS	F8, F2, F16
	FSUBS	F9, F3, F17
	FADDS	F6, F4, F18
	FADDS	F7, F5, F19
	FSUBS	F6, F4, F20
	FSUBS	F7, F5, F21

	FADDS	F14, F0, F0
	FADDS	F15, F1, F1
	FADDS	F18, F0, F0
	FADDS	F19, F1, F1

	FMULS	F14, F10, F24
	FMULS	F18, F12, F25
	FADDS	F25, F24, F24
	FADDS	F22, F24, F24
	FMULS	F15, F10, F25
	FMULS	F19, F12, F26
	FADDS	F26, F25, F25
	FADDS	F23, F25, F25

	FMULS	F17, F11, F26
	FMULS	F21, F13, F27
	FADDS	F27, F26, F26
	FMULS	F16, F11, F27
	FMULS	F20, F13, F28
	FADDS	F28, F27, F27
	FNEGS	F27, F27

	FSUBS	F26, F24, F2
	FSUBS	F27, F25, F3
	FADDS	F26, F24, F8
	FADDS	F27, F25, F9

	FMULS	F14, F12, F28
	FMULS	F18, F10, F29
	FADDS	F29, F28, F28
	FADDS	F22, F28, F28
	FMULS	F15, F12, F29
	FMULS	F19, F10, F30
	FADDS	F30, F29, F29
	FADDS	F23, F29, F29

	FMULS	F21, F11, F30
	FMULS	F17, F13, F31
	FSUBS	F31, F30, F30
	FMULS	F16, F13, F31
	FMULS	F20, F11, F26
	FSUBS	F26, F31, F31

	FADDS	F30, F28, F4
	FADDS	F31, F29, F5
	FSUBS	F30, F28, F6
	FSUBS	F31, F29, F7

	FMOVS	F0, (R6)
	FMOVS	F1, 4(R6)
	FMOVS	F2, 8(R6)
	FMOVS	F3, 12(R6)
	FMOVS	F4, 16(R6)
	FMOVS	F5, 20(R6)
	FMOVS	F6, 24(R6)
	FMOVS	F7, 28(R6)
	FMOVS	F8, 32(R6)
	FMOVS	F9, 36(R6)

	ADD	$1, R5, R5
	CMP	R5, R3
	BLT	kf_bfly5_loop

kf_bfly5_done:
	RET
